# Maintainer: aksr <aksr at t-com dot me>
pkgname=jump-git
_name=${pkgname%-git}
pkgver=r448.14b1fe7
pkgrel=1
pkgdesc="Quick and fuzzy directory jumper."
arch=('i686' 'x86_64')
url="https://github.com/gsamokovarov/jump"
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
source=("$_name::git+$url")
provides=("$_name")
conflicts=("$_name")
b2sums=('SKIP')

pkgver() {
  cd "$_name"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$_name"
  go build
}

package() {
  cd "$_name"
  install -Dm755 jump "$pkgdir/usr/bin/jump"
  install -Dm644 man/jump.1 "$pkgdir/usr/share/man/man1/jump.1"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$_name/README.md"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$_name/LICENSE"
}
