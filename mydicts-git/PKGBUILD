# Maintainer: Jax Young <jaxvanyang@gmail.com>

_pkgname=mydicts
pkgname="$_pkgname-git"
pkgver=r19.2a40607
pkgrel=1
pkgdesc="Dictionaries for mydict"
arch=(any)
url="https://github.com/jaxvanyang/mydicts"
license=('GPL-3.0-or-later')
makedepends=('git' 'just' 'cargo')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+$url.git")
sha256sums=('SKIP')

pkgver() {
	cd "$_pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$_pkgname"
	export RUSTUP_TOOLCHAIN=stable
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_pkgname"
	just
}

package() {
	cd "$_pkgname"
	just prefix="$pkgdir" install
}
